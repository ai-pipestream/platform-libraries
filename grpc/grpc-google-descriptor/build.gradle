plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.9.5'
    id 'maven-publish'
}

dependencies {
    // Note: BOM not needed - version catalog (libs) provides dependency management

    // protobuf and gRPC dependencies (versions from BOM)
    implementation libs.protobuf.java
    implementation libs.grpc.protobuf
    implementation libs.grpc.stub
    implementation libs.grpc.services
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

    // Proto files from grpc-services for import resolution (grpc/health/v1/health.proto)
    protobuf libs.grpc.services
    // Consume external protos
    protobuf libs.pipestream.protos
}

// group and version inherited from root project

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

compileJava {
    options.encoding = 'UTF-8'
}

def descriptorOutputDir = "${buildDir}/generated/grpc-descriptor"

// Configure protobuf plugin to generate descriptor with Google well-known types
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${libs.versions.protobuf.get()}"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${libs.versions.grpc.get()}"
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {
                outputSubDir = 'java'
            }
        }

        all().each { task ->
            if (task.sourceSet.name == 'main') {
                task.generateDescriptorSet = true
                task.descriptorSetOptions.includeImports = true
                task.descriptorSetOptions.path = "${descriptorOutputDir}/META-INF/grpc/services.dsc"
            }
        }
    }
}

// Include gRPC descriptor file for Google well-known types in JAR
jar {
    dependsOn generateProto, generateTestProto
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from('build/resources/main') {
        include 'META-INF/grpc/services.dsc'
    }
}

tasks.named('processResources') {
    dependsOn generateProto
    from(descriptorOutputDir) {
        include 'META-INF/grpc/services.dsc'
    }
}

// Fix task dependency warning
tasks.named('javadoc') {
    dependsOn generateProto, generateTestProto
}

description = 'Descriptor set for Google Protocol Buffer well-known types (Struct, Timestamp, Empty, etc.)'

// Publishing configuration inherited from root project

// Suppress enforced platform validation for publishing
tasks.withType(GenerateModuleMetadata) {
    suppressedValidationErrors.add('enforced-platform')
}
