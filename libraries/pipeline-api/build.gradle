plugins {
    alias(libs.plugins.java.library)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.jandex)
}

dependencies {
    implementation libs.quarkus.wiremock

    // Quarkus dependencies
    implementation libs.bundles.quarkus.common
    implementation(libs.bundles.quarkus.grpc)
    api project(':grpc:grpc-stubs')
    implementation libs.quarkus.jackson
    implementation 'io.quarkus:quarkus-hibernate-validator'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation 'io.quarkus:quarkus-cache'

    // Testing
    testImplementation libs.quarkus.junit5
}


java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

// Temporarily disable compilation of integrationTest sources for this module until tests are updated
tasks.matching { it.name == 'compileIntegrationTestJava' }.configureEach { it.enabled = false }

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

// Disable protobuf code generation - we only consume pre-built stubs
quarkus {
    buildForkOptions {
        systemProperty "quarkus.grpc.codegen.skip", "true"
    }
}

// Publishing configuration inherited from root project

// Suppress enforced platform validation for publishing
tasks.withType(GenerateModuleMetadata) {
    suppressedValidationErrors.add('enforced-platform')
}

description = 'Shared API types and helpers for Pipeline services'
