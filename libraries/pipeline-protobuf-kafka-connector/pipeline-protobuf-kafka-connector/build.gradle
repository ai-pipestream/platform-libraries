plugins {
    alias(libs.plugins.java.library)
    alias(libs.plugins.quarkus.extension)
}

description = 'Pipeline Protobuf Kafka Connector Runtime - Annotations only'

quarkusExtension {
    deploymentModule = 'pipeline-protobuf-kafka-connector-deployment'
    parentFirstArtifacts = [
        // Protobuf core
        'com.google.protobuf:protobuf-java',
        // gRPC - ALL artifacts must be parent-first for Quarkus gRPC extension to work
        'io.grpc:grpc-api',
        'io.grpc:grpc-stub',
        'io.grpc:grpc-protobuf',
        'io.grpc:grpc-core',
        'io.grpc:grpc-context',
        'io.grpc:grpc-netty',
        'io.grpc:grpc-util',
        'io.grpc:grpc-protobuf-lite',
        // Pipeline gRPC stubs
        'ai.pipestream:grpc-stubs',
        // SmallRye Reactive Mutiny and its dependencies (needed for reactive gRPC and messaging)
        'io.smallrye.reactive:mutiny',
        // JCTools - required by Mutiny for high-performance queues
        'org.jctools:jctools-core'
    ]
}

dependencies {
    // Pipeline BOM (includes Quarkus BOM transitively)
    implementation platform("ai.pipestream:pipeline-bom:${-> rootProject.version}")

    // Quarkus CDI - needed for the annotations
    implementation libs.quarkus.arc

    // Annotation processor for extension metadata
    annotationProcessor 'io.quarkus:quarkus-extension-processor'

    // Protobuf (for MessageLite interface)
    implementation libs.protobuf.java

    // SmallRye Reactive Messaging Kafka (for ClientCustomizer, @Channel, @Incoming)
    implementation 'io.smallrye.reactive:smallrye-reactive-messaging-kafka'

    // Apicurio Registry v3
    implementation 'io.apicurio:apicurio-registry-protobuf-serde-kafka:3.1.2'
}

// Workaround for Gradle 9: disable validateExtension which performs unsafe configuration resolution
tasks.matching { it.name == 'validateExtension' }.configureEach {
    enabled = false
}