pluginManagement {
    repositories {
        gradlePluginPortal()
    }
}

rootProject.name = 'platform-libraries'

dependencyResolutionManagement {
    repositories {
        mavenLocal {
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        mavenCentral()

        // Maven Central snapshots host ai.pipestream SNAPSHOTs
        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            mavenContent {
                snapshotsOnly()
            }
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        // Access Apache snapshots only for select dependencies when not on internal CI
        if (System.getenv('CI_ENV') != 'internal') {
            maven {
                url = uri('https://repository.apache.org/content/repositories/snapshots/')
                mavenContent {
                    snapshotsOnly()
                }
            }
        }
    }
}

// Include all subprojects - BOM first so it's available for other projects
include 'bom'

// devservices modules
include 'devservices:devservices'
include 'devservices:devservices-deployment'

// grpc modules
include 'grpc:grpc-stubs'
include 'grpc:grpc-google-descriptor'

// library modules
include 'libraries:pipeline-api'
include 'libraries:pipeline-commons'
include 'libraries:data-util'
include 'libraries:dynamic-grpc'
include 'libraries:dynamic-grpc-registration-clients'
include 'libraries:grpc-wiremock'
